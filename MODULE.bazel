module(name = "bazel-demo")

# --------------------------------------------------------- bazel
bazel_dep(
    name = "buildifier_prebuilt",
    version = "7.3.1",
    dev_dependency = True,
)

# --------------------------------------------------------- iOS
bazel_dep(name = "rules_xcodeproj", version = "3.3.0")
bazel_dep(
    name = "apple_support",
    version = "1.24.4",
    repo_name = "build_bazel_apple_support",
)
bazel_dep(
    name = "rules_swift",
    version = "3.3.0",
    repo_name = "build_bazel_rules_swift",
)
bazel_dep(
    name = "rules_apple",
    version = "4.2.0",
    repo_name = "build_bazel_rules_apple",
)
bazel_dep(name = "sourcekit_bazel_bsp", version = "0.3.0", repo_name = "sourcekit_bazel_bsp")

# bazel_dep(name = "swiftlint", version = "0.59.1", repo_name = "SwiftLint")
# bazel_dep(name = "aspect_rules_lint", version = "1.4.4")

# http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

# http_archive(
#     name = "swiftformat",
#     build_file_content = "filegroup(name = \"swiftformat\", srcs=[\"swiftformat_linux\"], visibility=[\"//visibility:public\"])",
#     patch_cmds = ["chmod u+x swiftformat_linux"],
#     sha256 = "f62813980c2848cb1941f1456a2a06251c2e2323183623760922058b98c70745",
#     url = "https://github.com/nicklockwood/SwiftFormat/releases/download/0.49.17/swiftformat_linux.zip",
# )

# http_archive(
#     name = "swiftformat_mac",
#     build_file_content = "filegroup(name = \"swiftformat_mac\", srcs=[\"swiftformat\"], visibility=[\"//visibility:public\"])",
#     patch_cmds = [
#         # On MacOS, `xattr -c` clears the "Unknown developer" warning when executing a fetched binary
#         "if command -v xattr > /dev/null; then xattr -c swiftformat; fi",
#         "chmod u+x swiftformat",
#         "mv swiftformat swiftformat_orig",
#         "echo \"$PWD/swiftformat_orig --config tools/.swiftlint \"'\"$@\"'\"\" >> swiftformat",
#         "chmod u+x swiftformat",
#     ],
#     sha256 = "978eaffdc3716bbc0859aecee0d83875cf3ab8d8725779448f0035309d9ad9f3",
#     url = "https://github.com/nicklockwood/SwiftFormat/releases/download/0.49.17/swiftformat.zip",
# )

bazel_dep(name = "rules_swift_package_manager", version = "1.10.0")

swift_deps = use_extension(
    "@rules_swift_package_manager//:extensions.bzl",
    "swift_deps",
)
swift_deps.from_package(
    resolved = "//:Package.resolved",
    swift = "//:Package.swift",
)
use_repo(
    swift_deps,
    "swift_package",
    "swiftpkg_swift_collections",
    "swiftpkg_swift_protobuf",
    "swiftpkg_swift_snapshot_testing",
)
# To add a package:
# 1. Update Package.swift
# 2. `swift package resolve` (from root (theoretically `bazel run @swift_package//:resolve` but didn't work for me))
# 3. `bazel mod tidy`

# NOTE: The name of the Bazel external repository for a Swift package is `swiftpkg_xxx` where
# `xxx` is the Swift package identity, lowercase, with punctuation replaced by `hyphen`. For
# example, the repository name for apple/swift-nio is `swiftpkg_swift_nio`.
# Use it like: "@swiftpkg_swift_nio//:NIO",

# ----------------------------------------------------------- Android
bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "rules_kotlin", version = "2.1.10")
bazel_dep(name = "rules_jvm_external", version = "6.9")
bazel_dep(name = "rules_android", version = "0.6.6")
bazel_dep(name = "bazel_skylib", version = "1.8.2")

# android_sdk_repository(name = "androidsdk", api_level = 34)

register_toolchains(
    "@rules_android//toolchains/android:android_default_toolchain",
    "@rules_android//toolchains/android_sdk:android_sdk_tools",
)

android_sdk_repository_extension = use_extension("@rules_android//rules/android_sdk_repository:rule.bzl", "android_sdk_repository_extension")
use_repo(android_sdk_repository_extension, "androidsdk")

register_toolchains("@androidsdk//:sdk-toolchain", "@androidsdk//:all")

bazel_dep(name = "rules_android_ndk", version = "0.1.3")

android_ndk_repository_extension = use_extension("@rules_android_ndk//:extension.bzl", "android_ndk_repository_extension")
use_repo(android_ndk_repository_extension, "androidndk")

register_toolchains("@androidndk//:all")

_COMPOSE_VERSION = "1.7.6"  # Dec 11, 2025

_KOTLIN_COMPILER_VERSION = "2.1.20"

maven = use_extension("@rules_jvm_external//:extensions.bzl", "maven")
maven.install(
    name = "maven_compose",
    aar_import_bzl_label = "@rules_android//android:rules.bzl",
    artifacts = [
        "org.jetbrains.kotlin:kotlin-compose-compiler-plugin-embeddable:{}".format(_KOTLIN_COMPILER_VERSION),
        "org.jetbrains.kotlin:kotlin-stdlib:{}".format(_KOTLIN_COMPILER_VERSION),
        "androidx.core:core-ktx:1.15.0",
        "androidx.appcompat:appcompat:1.7.0",
        "androidx.collection:collection-ktx:1.4.4",
        "androidx.compose.ui:ui:{}".format(_COMPOSE_VERSION),
        "androidx.compose.ui:ui-tooling:{}".format(_COMPOSE_VERSION),
        "androidx.compose.runtime:runtime:{}".format(_COMPOSE_VERSION),
        "androidx.compose.material:material:{}".format(_COMPOSE_VERSION),
        "androidx.compose.material:material-icons-extended:{}".format(_COMPOSE_VERSION),
        "androidx.compose.material3:material3:1.3.1",
        "androidx.javascriptengine:javascriptengine:1.0.0-beta01",
        "org.jetbrains.kotlinx:kotlinx-serialization-json:1.8.0",
        "androidx.navigation:navigation-compose:2.8.5",

        # Allows viewModel usage inside Compose
        "androidx.lifecycle:lifecycle-viewmodel-compose:2.8.3",
        # allows viewModelScope
        "androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.3",
        # lifecycleScope
        "androidx.lifecycle:lifecycle-runtime-compose:2.8.3",

        # allows using coroutines with ListenableFuture
        "org.jetbrains.kotlinx:kotlinx-coroutines-guava:1.6.0",

        # Fixes async killing issue
        "com.google.guava:guava:33.3.1-android",

        # Just resolutions
        "androidx.emoji2:emoji2-views-helper:1.3.0",
        "androidx.lifecycle:lifecycle-process:2.8.3",
        "androidx.lifecycle:lifecycle-runtime-ktx:2.8.3",
        "androidx.lifecycle:lifecycle-common-java8:2.8.3",

        # For uniffi
        "net.java.dev.jna:jna:5.18.1",

        # for network access
        "com.squareup.okhttp3:okhttp:4.12.0",

        # For tests
        "junit:junit:4.13.2",
    ],
    # fetch_sources = True,
    # REPIN=1 bazel run @maven_compose//:pin
    lock_file = "//:maven_compose.lock.json",
    repositories = [
        "https://maven.google.com",
        "https://repo1.maven.org/maven2",
    ],
    use_starlark_android_rules = True,
)
use_repo(maven, "maven_compose")

maven.install(
    name = "maven_compose_android",
    aar_import_bzl_label = "@rules_android//android:rules.bzl",
    artifacts = [
        "net.java.dev.jna:jna:5.18.1@aar",
    ],
    repositories = [
        "https://maven.google.com",
        "https://repo1.maven.org/maven2",
    ],
    use_starlark_android_rules = True,
)
use_repo(maven, "maven_compose_android")

# bazel_dep(name = "rules_java", version = "9.0.3")
# adds a maven + protobuf dependency as well, and this requires:
# maven.install(
#     artifacts = [
#         # "com.google.protobuf:protobuf-java-util:4.31.1",
#     ],
#     known_contributing_modules = [
#         "",
#         "bazel_worker_java",
#     ],
#     repositories = [
#         "https://maven.google.com",
#         "https://repo1.maven.org/maven2",
#     ],
# )
# use_repo(maven, "maven")

# ------------------------------------------------------------- JS
bazel_dep(name = "rules_nodejs", version = "6.6.0")

node = use_extension("@rules_nodejs//nodejs:extensions.bzl", "node")
node.toolchain(node_version = "20.11.1")

bazel_dep(name = "aspect_rules_js", version = "2.8.1")
bazel_dep(name = "aspect_rules_jest", version = "0.24.1")
bazel_dep(name = "aspect_bazel_lib", version = "2.19.3")

# Just so that you have access to pnpm for locking
pnpm = use_extension("@aspect_rules_js//npm:extensions.bzl", "pnpm", dev_dependency = True)
use_repo(pnpm, "pnpm")

code_tools_npm = use_extension("@aspect_rules_js//npm:extensions.bzl", "npm", dev_dependency = True)
code_tools_npm.npm_translate_lock(
    name = "code-tools-npm",
    npmrc = "//code-tools:.npmrc",
    pnpm_lock = "//code-tools:pnpm-lock.yaml",
    verify_node_modules_ignored = "//:.bazelignore",
)
use_repo(code_tools_npm, "code-tools-npm")

js_lib_npm = use_extension("@aspect_rules_js//npm:extensions.bzl", "npm", dev_dependency = True)
js_lib_npm.npm_translate_lock(
    name = "js-lib-npm",
    npmrc = "//js-lib:.npmrc",
    pnpm_lock = "//js-lib:pnpm-lock.yaml",
    verify_node_modules_ignored = "//:.bazelignore",
)
use_repo(js_lib_npm, "js-lib-npm")

js_site_npm = use_extension("@aspect_rules_js//npm:extensions.bzl", "npm", dev_dependency = True)
js_site_npm.npm_translate_lock(
    name = "js-site-npm",
    npmrc = "//js-site:.npmrc",
    pnpm_lock = "//js-site:pnpm-lock.yaml",
    verify_node_modules_ignored = "//:.bazelignore",
)
use_repo(js_site_npm, "js-site-npm")

example_shared_lib_jest_npm = use_extension("@aspect_rules_js//npm:extensions.bzl", "npm", dev_dependency = True)
example_shared_lib_jest_npm.npm_translate_lock(
    name = "example-shared-lib-jest-npm",
    npmrc = "//rust-code/client-shared/example-shared-lib/tests:.npmrc",
    pnpm_lock = "//rust-code/client-shared/example-shared-lib/tests:pnpm-lock.yaml",
    verify_node_modules_ignored = "//:.bazelignore",
)
use_repo(example_shared_lib_jest_npm, "example-shared-lib-jest-npm")

bazel_dep(name = "aspect_rules_ts", version = "3.7.1")
bazel_dep(name = "aspect_rules_swc", version = "2.5.0")

rules_ts_ext = use_extension("@aspect_rules_ts//ts:extensions.bzl", "ext", dev_dependency = True)
rules_ts_ext.deps()
use_repo(rules_ts_ext, "npm_typescript")

# --------------------------------------------------------- Rust

bazel_dep(name = "rules_rust", version = "0.67.0")

rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(
    edition = "2024",
    extra_target_triples = [
        "aarch64-apple-ios",
        "aarch64-apple-ios-sim",
        "aarch64-linux-android",
        "wasm32-unknown-unknown",
    ],
)
use_repo(rust, "rust_toolchains")

register_toolchains(
    "@rust_toolchains//:all",
    "@rules_rust//rust/private/dummy_cc_toolchain:dummy_cc_wasm32_toolchain",
)

crate = use_extension("@rules_rust//crate_universe:extension.bzl", "crate")

# We do this mainly due to buildifier formatting is a bit verbose
rust_package_versions = [
    # general
    ("chrono", "=0.4.42"),
    ("png", "=0.17.13"),
    ("regex", "=1.11.1"),
    ("send_wrapper", "=0.6.0"),
    ("thiserror", "=2.0.17"),
    ("unicode-segmentation", "=1.12.0"),
    ("weak-table", "=0.3.2"),

    # Editor / tree sitter things
    ("assert_cmd", "=2.0.16"),
    ("const_format", "=0.2.33"),
    ("serde_json", "=1.0.133"),
    ("tree-sitter", "=0.23.2"),
    ("tree-sitter-go", "=0.23.4"),
    ("tree-sitter-python", "=0.23.4"),
    ("tree-sitter-rust", "=0.23.2"),
    ("tree-sitter-scala", "=0.23.3"),
    ("tree-sitter-swift", "=0.7.0"),
    ("tree-sitter-typescript", "=0.23.2"),

    # uniffi / client things
    ("async-trait", "=0.1.89"),
    ("dashmap", "=6.1.0"),
    ("futures", "=0.3.31"),
    ("js-sys", "=0.3.82"),
    ("libsqlite3-sys", "=0.35.0"),
    ("parking_lot", "=0.12.5"),
    ("serde-wasm-bindgen", "=0.6.5"),
    ("tsify", "=0.5.6"),
    ("wasm-bindgen", "=0.2.105"),
    ("wasm-bindgen-futures", "=0.4.55"),
]

[
    crate.spec(
        package = package,
        version = version,
    )
    for (package, version) in rust_package_versions
]

crate.spec(
    features = ["derive"],
    package = "serde",
    version = "=1.0.215",
)
crate.spec(
    default_features = False,
    features = ["cli"],
    package = "uniffi",
    version = "=0.30.0",
)

# Useful if you want unstable features like LocalRuntime
# crate.annotation(
#     crate = "tokio",
#     rustc_flags = ["--cfg=tokio_unstable"],
# )
crate.spec(
    features = [
        "sync",
        "rt",
    ],
    package = "tokio",
    version = "=1.48.0",
)
crate.spec(
    # feature is transitive, so this will affect libsqlite3-sys
    # features = ["bundled"],
    package = "rusqlite",
    repositories = ["crates"],
    version = "=0.37.0",
)
crate.spec(
    features = ["bundled"],
    package = "rusqlite",
    repositories = ["crates-android"],
    version = "=0.37.0",
)
crate.from_specs(supported_platform_triples = [
    "aarch64-apple-darwin",
    # rustup target add aarch64-apple-ios aarch64-apple-ios-sim
    "aarch64-apple-ios",
    "aarch64-apple-ios-sim",
    "aarch64-linux-android",
    "wasm32-unknown-unknown",
])
crate.from_specs(
    name = "crates-android",
    supported_platform_triples = [
        "aarch64-apple-darwin",
        "aarch64-linux-android",
    ],
)
use_repo(crate, "crates", "crates-android")

# Use Cargo overhead for cargo patch overrides
# cargo_crate = use_extension("@rules_rust//crate_universe:extension.bzl", "crate")
# cargo_crate.from_cargo(
#     name = "cargo-crates",
#     cargo_lockfile = "//:Cargo.lock",
#     manifests = ["//:Cargo.toml"],
# )
# use_repo(cargo_crate, "cargo-crates")

# ------------------------------------------------------- Python

bazel_dep(name = "aspect_rules_py", version = "1.6.6")
bazel_dep(name = "rules_python", version = "1.6.3")

python = use_extension("@rules_python//python/extensions:python.bzl", "python")
python.toolchain(
    configure_coverage_tool = True,
    # Only set when you have mulitple toolchain versions.
    # is_default = True,
    python_version = "3.12",
)
use_repo(python, "python_versions")

pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip")
pip.parse(
    # envsubst = ["PIP_INDEX_URL"],
    hub_name = "pip",
    python_version = "3.12",
    requirements_lock = "//python_code:requirements.txt",
)
use_repo(pip, "pip")
